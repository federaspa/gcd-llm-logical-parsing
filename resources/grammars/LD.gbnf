#### Wrap rules, facts, and query in a valid JSON ####
root ::= "{" ws "\"facts\":" ws "[" ws factlist ws "]," ws "\"rules\":" ws "[" ws rulelist ws "]," ws "\"query\":" ws "\"" QUERY "\"" ws "}"
ws ::= | " " | "\n" [ \t]{0,5}

#### Facts list ####
factlist ::= | "\"" FACT "\"" (ws "," ws "\"" FACT "\""){0,30}

#### Rules list ####
rulelist ::= | "\"" RULE "\"" (ws "," ws "\"" RULE "\""){0,30}

#### ASP Program Elements with specific constraints ####
FACT ::= predicate "(" term ")."
RULE ::= head " :- " body "."
QUERY ::= simpleatom | "not " simpleatom

#### Head and Body structure ####
head ::= simpleatom | "not " simpleatom
body ::= literal | literal ", " literal | literal ", " literal ", " literal
literal ::= simpleatom | "not " simpleatom

#### Simple atom structure ####
simpleatom ::= predicate "(" term ")" | predicate "(" term ", " term ")"
predicate ::= [a-z][a-zA-Z0-9_]{0,15}
term ::= variable | constant

#### Basic terms ####
variable ::= [A-Z][a-zA-Z0-9_]{0,10}
constant ::= [a-z][a-zA-Z0-9_]{0,15}